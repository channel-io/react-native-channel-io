import groovy.json.JsonSlurper

def computeVersionName() {
    // dynamically retrieve version from package.json
    def slurper = new JsonSlurper()
    def json = slurper.parse(file('../package.json'), "utf-8")
    return json.version
}

buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.2'
    }
}

apply plugin: 'com.android.library'

android {
    namespace 'com.zoyi.channel.rn'
    compileSdk 34

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 34
        versionCode 1
        // get version name from package.json version
        versionName computeVersionName()
    }
    lint {
        abortOnError false
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'LICENSE'
        exclude 'NOTICE'
    }
}

repositories {
    mavenCentral()
    maven {
        url 'https://maven.google.com/'
        name 'Google'
    }
    maven {
        url 'https://maven.channel.io/maven2'
        name 'ChannelTalk'
    }
}

dependencies {
    implementation 'com.facebook.react:react-native'
    api 'io.channel:plugin-android:13.0.0'
}
